plugins {
    id 'fabric-loom' version "$loom_version"
    id 'org.moddedmc.wiki.toolkit' version "$wiki_version"
}

version = project.mod_version + "+mc." + project.minecraft_version
group = project.domain + "." + project.mod_id
def mod = project.mod_id

base {
    archivesName = project.mod_id
}

repositories {
    maven {
        name = 'JitPack'
        url = 'https://jitpack.io'
    }
}

loom {
    splitEnvironmentSourceSets()

    mods {
        create(mod) {
            sourceSet('main')
            sourceSet('client')
        }
    }
}

fabricApi {
    configureDataGeneration {
        client = true
        createRunConfiguration = true
    }
    configureTests {
        createSourceSet = true
        modId = "gametest"
        eula = true
    }
}

loom {
    runs {
        gameTestDev {
            name "GameTest Dev"
            inherit client
            source sourceSets.gametest
            runDir "build/gametest"
        }
    }
}

wiki {
    docs {
        create("magical-scepter") {
            root = file('docs')
        }
    }
    wikiAccessToken = System.getenv("GITHUB_TOKEN")
}

repositories {
    maven {
        name = "Terraformers"
        url = "https://maven.terraformersmc.com/releases"
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Mod menu to view mod meta data in development.
    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

    // Mixson for modifying json files in a mod compatible way.
    modImplementation include("com.github.ramixin:mixson:${project.mixson_version}")
}

processResources {
    inputs.property "id", mod_id
    inputs.property "version", mod_version
    inputs.property "loader_version", loader_version
    inputs.property "minecraft_version", minecraft_version
    inputs.property "mixson_version", mixson_version

    filesMatching("fabric.mod.json") {
        expand([
                "id"               : mod_id,
                "version"          : mod_version,
                "loader_version"   : loader_version,
                "minecraft_version": minecraft_version,
                "mixson_version"   : mixson_version
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}